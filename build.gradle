buildscript {
    repositories {
        jcenter()
        maven {
            url 'https://www.dimdev.org/maven/'
        }
        maven {
            url 'http://repo.spongepowered.org/maven'
        }
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.4'
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'org.spongepowered:mixingradle:0.4-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle.tweaker-client'
apply plugin: 'java'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'org.spongepowered.mixin'

def travisBuildNumber = System.getenv('TRAVIS_BUILD_NUMBER')
def versionSuffix = travisBuildNumber != null ? travisBuildNumber : 'SNAPSHOT'

group 'org.dimdev.rift'
version '1.0.1-' + versionSuffix
archivesBaseName = 'Rift'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    ivy {
        url 'https://libraries.minecraft.net/'
        layout 'pattern', {
            artifact '[organisation]/[module]/[revision]/[artifact]-[revision](-[classifier])(.[ext])'
            m2compatible = true
        }
    }
    maven {
        url 'https://www.dimdev.org/maven/'
    }
    maven {
        name = 'sponge'
        url = 'https://repo.spongepowered.org/maven/'
    }
    flatDir {
        dirs 'libs'
    }
}

dependencies {
    implementation('org.spongepowered:mixin:0.7.11-SNAPSHOT') { transitive = false }
    implementation('net.minecraft:launchwrapper:1.11') { transitive = false }

    implementation 'org.lwjgl:lwjgl:3.1.6:natives-linux'
    implementation 'org.lwjgl:lwjgl:3.1.6:natives-macos'
    implementation 'org.lwjgl:lwjgl:3.1.6:natives-windows'
    implementation 'org.lwjgl:lwjgl-glfw:3.1.6:natives-linux'
    implementation 'org.lwjgl:lwjgl-glfw:3.1.6:natives-macos'
    implementation 'org.lwjgl:lwjgl-glfw:3.1.6:natives-windows'
    implementation 'org.lwjgl:lwjgl-jemalloc:3.1.6:natives-linux'
    implementation 'org.lwjgl:lwjgl-jemalloc:3.1.6:natives-macos'
    implementation 'org.lwjgl:lwjgl-jemalloc:3.1.6:natives-windows'
    implementation 'org.lwjgl:lwjgl-openal:3.1.6:natives-linux'
    implementation 'org.lwjgl:lwjgl-openal:3.1.6:natives-macos'
    implementation 'org.lwjgl:lwjgl-openal:3.1.6:natives-windows'
    implementation 'org.lwjgl:lwjgl-opengl:3.1.6:natives-linux'
    implementation 'org.lwjgl:lwjgl-opengl:3.1.6:natives-macos'
    implementation 'org.lwjgl:lwjgl-opengl:3.1.6:natives-windows'
    implementation 'org.lwjgl:lwjgl-stb:3.1.6:natives-linux'
    implementation 'org.lwjgl:lwjgl-stb:3.1.6:natives-macos'
    implementation 'org.lwjgl:lwjgl-stb:3.1.6:natives-windows'
}

configurations {
    runtime.exclude module: 'launchwrapper'
    runtime.exclude module: 'lwjgl'
}

minecraft {
    version = '1.13'
    mappings = 'snapshot_3'
    runDir = 'run'
    tweakClass = 'org.dimdev.riftloader.launch.RiftLoaderTweaker'
    makeObfSourceJar = false
}

reobf {
    jar {
        mappingType = 'NOTCH'
        classpath = sourceSets.main.compileClasspath
    }
    shadowJar {
        mappingType = 'NOTCH'
        classpath = sourceSets.main.compileClasspath
    }
}

mixin {
    defaultObfuscationEnv notch
    add sourceSets.main, 'mixins.rift.refmap.json'
}

sourceSets {
    main
    debug {
        compileClasspath += main.compileClasspath
    }
}

jar {
    manifest.attributes(
            'Main-Class': 'org.dimdev.rift.Installer'
    )
}

task devJar(type: Jar, dependsOn: classes) {
    classifier 'dev'
    from sourceSets.main.output
}

shadowJar {
    exclude 'LICENSE.txt', 'dummyThing'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives jar
    archives devJar
    archives shadowJar
    archives sourcesJar
}

runClient.group 'other' // This is broken
genIntellijRuns.group 'other' // Use 'Start.java' in the 'debug' module instead
